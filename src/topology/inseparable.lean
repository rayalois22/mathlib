/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import topology.continuous_on
import data.setoid.basic
import tactic.tfae

/-!
# Inseparable points in a topological space

In this file we define

* `inseparable x y`: a predicate saying that two points in a topological space have the same
  neighbourhoods; equivalently, they can't be separated by an open set;

* `inseparable_setoid X`: same relation, as a `setoid`;

* `separation_quotient X`: the quotient of `X` by its `inseparable_setoid`.

We also prove various basic properties of the relation `inseparable`.

## Notations

- `~` is used as a local notation for `inseparable`;
- `ğ“ x` is the neighbourhoods filter of a point `x`, defined elsewhere.

## Tags

topological space, separation setoid
-/

open set filter function
open_locale topological_space

variables {X Y : Type*} [topological_space X] [topological_space Y] {x y z : X}
  {s : set X} {f : X â†’ Y}

lemma nhds_le_nhds_tfae (x y : X) :
  tfae [ğ“ x â‰¤ ğ“ y,
    pure x â‰¤ ğ“ y,
    âˆ€ s : set X, is_open s â†’ y âˆˆ s â†’ x âˆˆ s,
    âˆ€ s : set X, is_closed s â†’ x âˆˆ s â†’ y âˆˆ s,
    y âˆˆ closure ({x} : set X),
    cluster_pt y (pure x)] :=
begin
  tfae_have : 1 â†’ 2, from (pure_le_nhds _).trans,
  tfae_have : 2 â†’ 3, from Î» h s hso hy, h (hso.mem_nhds hy),
  tfae_have : 3 â†’ 4, from Î» h s hsc hx, of_not_not $ Î» hy, h sá¶œ hsc.is_open_compl hy hx,
  tfae_have : 4 â†’ 5, from Î» h, h _ is_closed_closure (subset_closure $ mem_singleton _),
  tfae_have : 5 â†” 6, by rw [mem_closure_iff_cluster_pt, principal_singleton],
  tfae_have : 6 â†’ 1,
  { refine Î» h, (nhds_basis_opens _).ge_iff.2 _,
    rintro s âŸ¨hy, hoâŸ©,
    have := cluster_pt_iff.1 h (ho.mem_nhds hy) (mem_pure.2 $ mem_singleton _),
    exact ho.mem_nhds (inter_singleton_nonempty.1 this) },
  tfae_finish
end

lemma nhds_le_nhds_iff_pure : ğ“ x â‰¤ ğ“ y â†” pure x â‰¤ ğ“ y :=
(nhds_le_nhds_tfae x y).out 0 1

lemma nhds_le_nhds_iff_open : ğ“ x â‰¤ ğ“ y â†” âˆ€ â¦ƒs : set Xâ¦„, is_open s â†’ y âˆˆ s â†’ x âˆˆ s :=
(nhds_le_nhds_tfae x y).out 0 2

lemma nhds_le_nhds_iff_closed : ğ“ x â‰¤ ğ“ y â†” âˆ€ â¦ƒs : set Xâ¦„, is_closed s â†’ x âˆˆ s â†’ y âˆˆ s :=
(nhds_le_nhds_tfae x y).out 0 3

lemma nhds_le_nhds_iff_mem_closure : ğ“ x â‰¤ ğ“ y â†” y âˆˆ closure ({x} : set X) :=
(nhds_le_nhds_tfae x y).out 0 4

lemma nhds_le_nhds_iff_cluster_pt : ğ“ x â‰¤ ğ“ y â†” cluster_pt y (pure x) :=
(nhds_le_nhds_tfae x y).out 0 5

lemma nhds_le_nhds_of_nhds_within (hâ‚ : ğ“[s] x â‰¤ ğ“[s] y) (hâ‚‚ : x âˆˆ s) : ğ“ x â‰¤ ğ“ y :=
nhds_le_nhds_iff_pure.2 $
calc pure x â‰¤ ğ“[s] x : le_inf (pure_le_nhds _) (le_principal_iff.2 hâ‚‚)
        ... â‰¤ ğ“[s] y : hâ‚
        ... â‰¤ ğ“ y    : inf_le_left

lemma nhds_le_nhds_of_continuous_at (h : ğ“ x â‰¤ ğ“ y) (hy : continuous_at f y) :
  ğ“ (f x) â‰¤ ğ“ (f y) :=
nhds_le_nhds_iff_pure.2 $ Î» s hs, mem_pure.2 $ mem_preimage.1 $ mem_of_mem_nhds $ hy.mono_left h hs

/-- Two points `x` and `y` in a topological space are `inseparable` if any of the following
equivalent properties hold:

- `ğ“ x = ğ“ y`; we use this property as the definition;
- for any open set `s`, `x âˆˆ s â†” y âˆˆ s`, see `inseparable_iff_open`;
- for any closed set `s`, `x âˆˆ s â†” y âˆˆ s`, see `inseparable_iff_closed`;
- `x âˆˆ closure {y}` and `y âˆˆ closure {x}`, see `inseparable_iff_mem_closure`.
-/
def inseparable (x y : X) : Prop := ğ“ x = ğ“ y

local infix ` ~ ` := inseparable

lemma inseparable_def : x ~ y â†” ğ“ x = ğ“ y := iff.rfl

lemma inseparable_iff_open : x ~ y â†” âˆ€ s : set X, is_open s â†’ (x âˆˆ s â†” y âˆˆ s) :=
by simp only [inseparable, le_antisymm_iff, nhds_le_nhds_iff_open, â† forall_and_distrib, â† iff_def,
  iff.comm]

lemma not_inseparable_iff_open : Â¬(x ~ y) â†” âˆƒ s : set X, is_open s âˆ§ xor (x âˆˆ s) (y âˆˆ s) :=
by simp [inseparable_iff_open, â† xor_iff_not_iff]

lemma inseparable_iff_closed : x ~ y â†” âˆ€ s : set X, is_closed s â†’ (x âˆˆ s â†” y âˆˆ s) :=
by simp only [inseparable, le_antisymm_iff, nhds_le_nhds_iff_closed, â† forall_and_distrib,
  â† iff_def]

lemma inseparable_iff_mem_closure :
  x ~ y â†” x âˆˆ closure ({y} : set X) âˆ§ y âˆˆ closure ({x} : set X) :=
le_antisymm_iff.trans $ by simp only [nhds_le_nhds_iff_mem_closure, and_comm]

lemma inseparable_of_nhds_within_eq (hx : x âˆˆ s) (hy : y âˆˆ s) (h : ğ“[s] x = ğ“[s] y) : x ~ y :=
le_antisymm (nhds_le_nhds_of_nhds_within h.le hx) (nhds_le_nhds_of_nhds_within h.ge hy)

lemma inducing.inseparable_iff (hf : inducing f) : f x ~ f y â†” x ~ y :=
by simp only [inseparable_iff_mem_closure, hf.closure_eq_preimage_closure_image, image_singleton,
  mem_preimage]

namespace inseparable

@[refl] lemma refl (x : X) : x ~ x := eq.refl (ğ“ x)

lemma rfl : x ~ x := refl x

@[symm] lemma symm (h : x ~ y) : y ~ x := h.symm

@[trans] lemma trans (hâ‚ : x ~ y) (hâ‚‚ : y ~ z) : x ~ z := hâ‚.trans hâ‚‚

lemma nhds_eq (h : x ~ y) : ğ“ x = ğ“ y := h

lemma mem_open_iff (h : x ~ y) (hs : is_open s) : x âˆˆ s â†” y âˆˆ s :=
inseparable_iff_open.1 h s hs

lemma mem_closed_iff (h : x ~ y) (hs : is_closed s) : x âˆˆ s â†” y âˆˆ s :=
inseparable_iff_closed.1 h s hs

lemma map_of_continuous_at (h : x ~ y) (hx : continuous_at f x) (hy : continuous_at f y) :
  f x ~ f y :=
le_antisymm (nhds_le_nhds_of_continuous_at h.le hy) (nhds_le_nhds_of_continuous_at h.ge hx)

lemma map (h : x ~ y) (hf : continuous f) : f x ~ f y :=
h.map_of_continuous_at hf.continuous_at hf.continuous_at

end inseparable

lemma is_closed.not_inseparable (hs : is_closed s) (hx : x âˆˆ s) (hy : y âˆ‰ s) : Â¬x ~ y :=
Î» h, hy $ (h.mem_closed_iff hs).1 hx

lemma is_open.not_inseparable (hs : is_open s) (hx : x âˆˆ s) (hy : y âˆ‰ s) : Â¬x ~ y :=
Î» h, hy $ (h.mem_open_iff hs).1 hx

variable (X)

/-- A `setoid` version of `inseparable`, used to define the `separation_quotient`. -/
def inseparable_setoid : setoid X :=
{ r := (~),
  .. setoid.comap ğ“ âŠ¥ }

/-- The quotient of a topological space by its `inseparable_setoid`. This quotient is guaranteed to
be a Tâ‚€ space. -/
@[derive topological_space]
def separation_quotient := quotient (inseparable_setoid X)

variable {X}

namespace separation_quotient

/-- The natural map from a topological space to its separation quotient. -/
def mk : X â†’ separation_quotient X := quotient.mk'

lemma quotient_map_mk : quotient_map (mk : X â†’ separation_quotient X) :=
quotient_map_quot_mk

lemma continuous_mk : continuous (mk : X â†’ separation_quotient X) :=
continuous_quot_mk

@[simp] lemma mk_eq_mk : mk x = mk y â†” x ~ y := quotient.eq'

lemma surjective_mk : surjective (mk : X â†’ separation_quotient X) :=
surjective_quot_mk _

@[simp] lemma range_mk : range (mk : X â†’ separation_quotient X) = univ :=
surjective_mk.range_eq

lemma preimage_image_mk_open (hs : is_open s) : mk â»Â¹' (mk '' s) = s :=
begin
  refine subset.antisymm _ (subset_preimage_image _ _),
  rintro x âŸ¨y, hys, hxyâŸ©,
  exact ((mk_eq_mk.1 hxy).mem_open_iff hs).1 hys
end

lemma is_open_map_mk : is_open_map (mk : X â†’ separation_quotient X) :=
Î» s hs, quotient_map_mk.is_open_preimage.1 $ by rwa preimage_image_mk_open hs

lemma preimage_image_mk_closed (hs : is_closed s) : mk â»Â¹' (mk '' s) = s :=
begin
  refine subset.antisymm _ (subset_preimage_image _ _),
  rintro x âŸ¨y, hys, hxyâŸ©,
  exact ((mk_eq_mk.1 hxy).mem_closed_iff hs).1 hys
end

lemma inducing_mk : inducing (mk : X â†’ separation_quotient X) :=
âŸ¨le_antisymm (continuous_iff_le_induced.1 continuous_mk)
  (Î» s hs, âŸ¨mk '' s, is_open_map_mk s hs, preimage_image_mk_open hsâŸ©)âŸ©

lemma is_closed_map_mk : is_closed_map (mk : X â†’ separation_quotient X) :=
inducing_mk.is_closed_map $ by { rw [range_mk], exact is_closed_univ }

lemma map_mk_nhds : map mk (ğ“ x) = ğ“ (mk x) :=
by rw [inducing_mk.nhds_eq_comap, map_comap_of_surjective surjective_mk]

end separation_quotient
